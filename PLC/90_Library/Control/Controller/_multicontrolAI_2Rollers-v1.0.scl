FUNCTION_BLOCK "_multicontrolAI_2Rollers"
TITLE = Function:Multicontrol AI for two motoroller
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : cyanezf
FAMILY : 'Control/Controller'
NAME : _multicontrolAI_2Rollers
VERSION : 1.0
   VAR_INPUT 
      controlOn { ExternalWritable := 'False'} : Bool;   // IR: 1=Control on
      emrgStopOk { ExternalWritable := 'False'} : Bool;   // IR: 1=Emergency ok
      runForward { ExternalWritable := 'False'} : Bool;   // IR: 1=Run forward
      runBackward { ExternalWritable := 'False'} : Bool;   // IR: 1=Run backward
      AI_R1_speed { ExternalWritable := 'False'} : SInt;   // IR: Current speed +-% - Roller 1
      AI_R2_speed { ExternalWritable := 'False'} : SInt;   // IR: Current speed +-% - Roller 2
      SP_speed { ExternalWritable := 'False'} : Real;   // IR: Setpoint speed +-%
      MAX_SPEED { ExternalWritable := 'False'} : Real;   // IR: Max real speed
      opt_use2Roller { ExternalWritable := 'False'} : Bool := 0;   // IR: 0=Use only one motor; 1=User 2 motors
   END_VAR

   VAR_OUTPUT 
      isRunning { ExternalWritable := 'False'} : Bool;   // OR: 1=Is running
      direction { ExternalWritable := 'False'} : Bool;   // OR: 0=Forward; 1=Backward
      speed { ExternalWritable := 'False'} : Real;   // OR: Current speed
      AO_R1_speed { ExternalWritable := 'False'} : SInt;   // OR: Output speed to roller 1
      AO_R2_speed { ExternalWritable := 'False'} : SInt;   // OR: Output speed to roller 2
   END_VAR

   VAR_TEMP 
      ok : Bool;
      tmpSpeed : SInt;
   END_VAR

   VAR CONSTANT 
      MAX_SPEED_PER : SInt := 100;
      NO_SPEED_PER : SInt := 0;
   END_VAR


BEGIN
	(*--
	# Log
	| version | date | author | description |
	|:-------:|:----:|:------:|:------------|
	| v1.0 | 2025-07-28 | cyanezf | First version |
	
	# Dependencies
	| Dependency |
	|:-----------|
	
	# Description
	This function controls Interroll Multicontrol AI for two rollers.
	
	#
	_Use [Markdown Live Preview](https://markdownlivepreview.com/) to watch this doc._
	--*)
	
	// Speed to %
	// MAX_SPEED -> 100%
	// #SP_speed  -> ?
	// ? = #SP_speed * 100% / MAX_SPEED
	#tmpSpeed := REAL_TO_SINT(#SP_speed * #MAX_SPEED_PER / #MAX_SPEED);
	
	// Ok
	#ok := #controlOn AND #emrgStopOk AND #tmpSpeed <> #NO_SPEED_PER;
	
	// Output speed
	IF #runForward AND NOT #runBackward AND #ok THEN
	  
	  IF #tmpSpeed > #MAX_SPEED_PER THEN
	    #tmpSpeed := #MAX_SPEED_PER;
	  END_IF;
	  
	ELSIF #runBackward AND NOT #runForward AND #ok THEN
	  
	  IF #tmpSpeed > #MAX_SPEED_PER THEN
	    #tmpSpeed := - #MAX_SPEED_PER;
	  ELSE
	    #tmpSpeed := - #tmpSpeed;
	  END_IF;
	  
	ELSE
	  #tmpSpeed := #NO_SPEED_PER;
	  
	END_IF;
	  
	#AO_R1_speed := #tmpSpeed;
	
	IF #opt_use2Roller THEN
	  #AO_R2_speed := #tmpSpeed;
	END_IF;
	
	
	// Is running and direction
	IF #opt_use2Roller THEN
	  #isRunning := #AI_R1_speed <> #NO_SPEED_PER AND #AI_R2_speed <> #NO_SPEED_PER;
	  #direction := #AI_R1_speed < #NO_SPEED_PER AND #AI_R2_speed < #NO_SPEED_PER;
	ELSE
	  #isRunning := #AI_R1_speed <> #NO_SPEED_PER;
	  #direction := #AI_R1_speed < #NO_SPEED_PER;
	END_IF;
	
	// % to speed
	// 100%     -> MAX_PSEED
	// #AI_speed -> ?
	// ? = #AI_speed * MAX_SPEED / 100%
	IF #opt_use2Roller THEN
	  #speed := ((SINT_TO_REAL(#AI_R1_speed) * #MAX_SPEED / #MAX_SPEED_PER) + (SINT_TO_REAL(#AI_R2_speed) * #MAX_SPEED / #MAX_SPEED_PER)) / 2.0;
	ELSE
	  #speed := SINT_TO_REAL(#AI_R1_speed) * #MAX_SPEED / #MAX_SPEED_PER;
	END_IF;
	
	// Save RLO
	ENO := true;
	
END_FUNCTION_BLOCK

