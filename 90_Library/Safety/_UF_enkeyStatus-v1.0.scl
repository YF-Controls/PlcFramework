FUNCTION "_UF_enkeyStatus" : Int
TITLE = Function:Enabling key status
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : cyanezf
FAMILY : Safety
NAME : _UF_enKeyStatus
VERSION : 1.0

   VAR_INPUT 
      swKey : Bool;   // 1=Software key in enable position
      F_key : Bool;   // 1=Safety key in enable position
      passivation : Bool;   // 1= QBAD from F-DB module of this input is triggered (Passivated)
   END_VAR

   VAR CONSTANT 
      F_STATUS_0000_DISABLED : Int := 0;   // F-Status: 0x0000 - Disabled
      F_STATUS_0001_READY : Int := 1;   // F-Status: 0x0001 - Ready
      F_STATUS_8001_PASSIVATION : Int := 16#8001;   // F-Status: 0x8001 - Error - F-object passition
      F_STATUS_8005_KEY_MODE_MISMATCH_01 : Int := 16#8005;   // F-Status: 0x8005 - Error - Key mode mismatch 01 (swKey == 0, F_key == 1)
      F_STATUS_8006_KEY_MODE_MISMATCH_10 : Int := 16#8006;   // F-Status: 0x8006 - Error - Key mode mismatch 10 (swKey == 1, F_key == 0)
   END_VAR


BEGIN
(*--

# Log
| version | date | author | description |
|:-------:|:----:|:------:|:------------|
| v1.0 | 2023-10-17 | cyanezf | First version |

# Dependencies
| Dependency |
|:-----------|

# Description
This function returns a processed value
for a physical Safety key and harwdware
enabling bit

## RLO and return
| RLO | value | description |
|:---:|:-----:|:------------|
| TRUE | 0 | Disabled
| TRUE | 1 | Ready/ok
| TRUE | 0x8001 | Channel Passivation
| TRUE | 0x8005 | Key mode mismatch 01 (swKey == 0, F_key == 1) |
| TRUE | 0x8006 | Key mode mismatch 10 (swKey == 1, F_key == 0) |

#
_Use [Markdown Live Preview](https://markdownlivepreview.com/) to watch this doc._
--*)
	
	// Check conditions
	IF NOT #swKey THEN
	  #_UF_enkeyStatus := #F_STATUS_0000_DISABLED;
	  
	ELSIF #passivation THEN
	  #_UF_enkeyStatus := #F_STATUS_8001_PASSIVATION;
	  
	ELSIF NOT #swKey AND #F_key THEN
	  #_UF_enkeyStatus := #F_STATUS_8005_KEY_MODE_MISMATCH_01;
	  
	ELSIF #swKey AND NOT #F_key THEN
	  #_UF_enkeyStatus := #F_STATUS_8006_KEY_MODE_MISMATCH_10;
	  
	ELSE
	  #_UF_enkeyStatus := #F_STATUS_0001_READY;
	  
	END_IF;
	
	// Save RLO
	ENO := true;
	
END_FUNCTION

